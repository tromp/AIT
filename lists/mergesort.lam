-- Sort a list of binary lists lexicographically (mergesort version).
-- [] < 0:* < 1:*
-- by Artyom Bologov (https://github.com/aartaka)
let
  true = \x\y.x;
  false = \x\y.y;
  nil = false;

  bytelt = \a\b.a (\ahead\atail\z.
                        ((b true) ahead (ahead false true))
                        (bytelt atail (b false))
                        ahead)
                      true;

  cons = \a\b\p.p a b;

  sortmerge = \list\pred\x.
    list
    (let go = \head\tail\x.
      (pred head (x true))
      (cons head (tail go x))
      (cons (x true) (cons head tail)) in go)
    (cons x nil);
  sort = \pred\list.
    list (let go = 
           (\head\tail\z.
             sortmerge (tail go z) pred head) in go) nil;
  main = \io.sort bytelt io
in main

-- echo -n abracadabra | ./blc run8 mergesort.lam
-- aaaaabbcdrr
