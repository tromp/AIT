-- Sort a list of binary lists lexicographically (mergesort version).
-- [] < 0:* < 1:*
-- by Artyom Bologov (https://github.com/aartaka)
let
  true = \x\y.x;
  false = \x\y.y;
  not = \b\t\f.b f t;
  nil = false;
  cons = \a\b\p.p a b;

  leq = \a\b.a (\ahead\atail\z.
                  (b true ahead (not ahead))
                  (leq atail (b false))
                  ahead)
               true;

  insert = \x. let insertx = \list.
    list
    (\head\tail\lx.
      leq head x
      (cons head (insertx tail ))
      (cons x list) )
    (cons x list)
  in insertx;

  sort = \list.		-- 300 bits
    list (let go = 
           (\head\tail\z.
             insert head (tail go z))
          in go) nil;

in sort

-- echo -n abracadabra | ./blc run8 sort.lam
-- aaaaabbcdrr
